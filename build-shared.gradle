
// These properties are in their own file to ensure that they're kept in sync between the
// main Java build (build.gradle) and the Android CI build (build-android-ci.gradle).

allprojects {
    group = 'com.launchdarkly'
    version = "${version}"
    archivesBaseName = "launchdarkly-java-sdk-common"
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

ext {
    sdkBasePackage = "com.launchdarkly.sdk"
    sdkBaseName = "launchdarkly-java-sdk-common"
}

ext.versions = [
    "gson": "2.7",
    "jacksonCore": "2.10.5",
    "jacksonDatabind": "2.10.5.1"
]

ext.libraries = [:]

configurations {
    privateImplementation
}

dependencies {
    // The Gson and Jackson dependencies go into a custom configuration here, instead of
    // the regular "implementation" configuration. We don't want them to be visible at
    // all in the module's published dependencies - not even in "runtime" scope - because:
    //
    // 1. For Gson: While java-sdk-common does need Gson in order to work, the
    // LaunchDarkly SDKs that use java-sdk-common have different strategies for packaging
    // Gson. The Android SDK exposes it as a regular dependency; the Java server-side SDK
    // embeds and shades Gson and does not expose it as a dependency. So we are leaving
    // it up to the SDK to provide Gson in some way.
    //
    // 2. For Jackson: The SDKs do not use, require, or embed Jackson; we provide the
    // LDJackson class as a convenience for applications that do use Jackson, and it is
    // only usable if the application already has Jackson in its classpath. So we do not
    // want Jackson to show up as a transitive  dependency.
    privateImplementation "com.google.code.gson:gson:${versions.gson}"
    privateImplementation "com.fasterxml.jackson.core:jackson-core:${versions.jacksonCore}"
    privateImplementation "com.fasterxml.jackson.core:jackson-databind:${versions.jacksonDatabind}"

    testImplementation configurations.privateImplementation
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "junit:junit:4.12"
}
