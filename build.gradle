
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    id "java"
    id "java-library"
    id "checkstyle"
    id "jacoco"
    id "signing"
    id "maven-publish"
    id "de.marcphilipp.nexus-publish" version "0.3.0"
    id "io.codearte.nexus-staging" version "0.21.2"
    id "org.ajoberstar.git-publish" version "2.1.3"
    id "idea"
}

repositories {
    mavenLocal()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    mavenCentral()
}

configurations.all {
    // check for updates every build for dependencies with: 'changing: true'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

apply from: 'build-shared.gradle'

checkstyle {
    configFile file("${project.rootDir}/checkstyle.xml")
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    // Force the Javadoc build to fail if there are any Javadoc warnings. See: https://discuss.gradle.org/t/javadoc-fail-on-warning/18141/3
    // The '-quiet' as second argument is actually a hack,
    // since the one parameter addStringOption doesn't seem to
    // work, we extra add '-quiet', which is added anyway by
    // gradle. See https://github.com/gradle/gradle/issues/2354
    // See JDK-8200363 (https://bugs.openjdk.java.net/browse/JDK-8200363)
    // for information about the -Xwerror option.
    options.addStringOption('Xwerror', '-quiet')
}

artifacts {
    archives jar, sourcesJar, javadocJar
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

jacocoTestReport { // code coverage report
	reports {
		xml.enabled
		csv.enabled true
		html.enabled true
	}
}

jacocoTestCoverageVerification {
	// See notes in CONTRIBUTING.md on code coverage. Unfortunately we can't configure line-by-line code
	// coverage overrides within the source code itself, because Jacoco operates on bytecode.
	violationRules { rules ->
		def knownMissedLinesForMethods = [
			// The key for each of these items is the complete method signature minus the "com.launchdarkly.sdk." prefix.
			"EvaluationReason.error(com.launchdarkly.sdk.EvaluationReason.ErrorKind)": 1,
			"EvaluationReasonTypeAdapter.parse(com.google.gson.stream.JsonReader)": 1,
			"EvaluationDetailTypeAdapterFactory.EvaluationDetailTypeAdapter.read(com.google.gson.stream.JsonReader)": 1,
			"LDValue.equals(java.lang.Object)": 1,
			"LDValueTypeAdapter.read(com.google.gson.stream.JsonReader)": 3,
			"json.JsonSerialization.getDeserializableClasses()": -1
		]
		
		knownMissedLinesForMethods.each { partialSignature, maxMissedLines ->
			if (maxMissedLines > 0) {  // < 0 means skip entire method
				rules.rule {
					element = "METHOD"
					includes = [ "com.launchdarkly.sdk." + partialSignature ]
					limit {
						counter = "LINE"
						value = "MISSEDCOUNT"
						maximum = maxMissedLines
					}
				}
			}
		}
		
		// General rule that we should expect 100% test coverage; exclude any methods that have overrides above
		rule {
			element = "METHOD"
			limit {
				counter = "LINE"
				value = "MISSEDCOUNT"
				maximum = 0
			}
			excludes = knownMissedLinesForMethods.collect { partialSignature, maxMissedLines ->
				"com.launchdarkly.sdk." + partialSignature
			}
		}
	}
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

nexusStaging {
    packageGroup = "com.launchdarkly"
    numberOfRetries = 40 // we've seen extremely long delays in closing repositories
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'com.launchdarkly'
            artifactId = 'launchdarkly-java-sdk-common'

            artifact sourcesJar
            artifact javadocJar
            
            pom {
                name = 'launchdarkly-java-sdk-common'
                description = 'LaunchDarkly SDK Java Common Classes'
                url = 'https://github.com/launchdarkly/java-sdk-common'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'LaunchDarkly'
                        email = 'team@launchdarkly.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/launchdarkly/java-sdk-common.git'
                    developerConnection = 'scm:git:ssh:git@github.com:launchdarkly/java-sdk-common.git'
                    url = 'https://github.com/launchdarkly/java-sdk-common'
                }
            }   
                     
                // We are deliberately hiding our dependencies in the pom, for the following reasons:
                //
                // 1. Gson: While java-sdk-common does need Gson in order to work, the LaunchDarkly SDKs that use
                // java-sdk-common have different strategies for packaging Gson. The Android SDK exposes it as a
                // regular dependency; the Java server-side SDK embeds and shades Gson and does not expose it as
                // a dependency. So we are leaving it up to the SDK to provide Gson in some way.
                //
                // 2. Jackson: The SDKs do not use, require, or embed Jackson; we provide the LDJackson class as
                // a convenience for applications that do use Jackson. So we do not want it to be a transitive
                // dependency.
            pom.withXml {
                asNode().dependencies.forEach { it.value = "" }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

nexusPublishing {
    clientTimeout = java.time.Duration.ofMinutes(2) // we've seen extremely long delays in creating repositories
    repositories {
        sonatype {
            username = ossrhUsername
            password = ossrhPassword            
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { !"1".equals(project.findProperty("LD_SKIP_SIGNING")) } // so we can build jars for testing in CI
}

gitPublish {
    repoUri = 'git@github.com:launchdarkly/java-sdk-common.git'
    branch = 'gh-pages'
    contents {
        from javadoc
    }
    commitMessage = 'publishing javadocs'
}
